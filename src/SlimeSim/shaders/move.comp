#version 430 core

#pragma optimize(on)

layout(local_size_x = {LocalSizeX}) in;

struct ShaderConfig {
    int agentsCount;
    int width;
    int height;
    float dt;
    float t;
    int generationDuration;
    float initialEnergy;
    float plantEnergy;
    float killEnergy;
    int plantRegrowDuration;
    int trackedIdx;
    float blueMaxVelocity;
    float redMaxVelocity;
    int pad0;
    int pad1;
    int pad2;
};

struct Agent
{
    vec2 position;
    float angle;
    uint type;
    float energy;
    uint age;
    int state;
    int nnOffset;
    int meals;
    int deaths;
    float energySpent;
    int flag;
    ivec2 currPixel;
    ivec2 prevPixel;
    float memory0;
    float memory1;
    float deltaAngle;
    uint survivalDuration;
};

layout(std430, binding = 0) buffer ConfigBuffer {
    ShaderConfig config;
};

layout(std430, binding = 1) buffer AgentsBuffer {
    Agent agents[];
};

layout(rgba32f, binding = 2) uniform image2D inGreen;
layout(rgba32f, binding = 3) uniform image2D inBlue;
layout(rgba32f, binding = 4) uniform image2D inRed;

layout(std430, binding = 5) buffer NetworkBuffer {
    float network[];
};

layout(std430, binding = 6) writeonly buffer TrackingBuffer
{
    Agent tracked;
};

const float M_PI        = 3.14159265358979323846;

void torus(inout vec2 pos)
{
    if (pos.x > config.width)
        pos.x -= config.width;
    else if (pos.x < 0)
        pos.x += config.width;

    if (pos.y > config.height)
        pos.y -= config.height;
    else if (pos.y < 0)
        pos.y += config.height;
}

void itorus(inout ivec2 pos)
{
    if (pos.x > config.width)
        pos.x -= config.width;
    else if (pos.x < 0)
        pos.x += config.width;

    if (pos.y > config.height)
        pos.y -= config.height;
    else if (pos.y < 0)
        pos.y += config.height;
}

uint hash(uint state)
{
    state ^= 2747636419u;
    state *= 2654435769u;
    state ^= state >> 16;
    state *= 2654435769u;
    state ^= state >> 16;
    state *= 2654435769u;
    return state;
}

float scaleToRange01(uint state)
{
    return state / 4294967295.0;
}

float get_random_float_01(Agent agent, uint seed)
{
     uint random = hash(uint(agent.position.y * config.width + agent.position.x) + hash(gl_GlobalInvocationID.x + int(config.t) * seed));
     float random01 = scaleToRange01(random);
     return random01;
}

vec3 sense(Agent agent, float sensorAngle, float sensorDist, int sensorSize)
{
    float sensingAngle = agent.angle + sensorAngle;
    vec2 sensingDir = vec2(cos(sensingAngle),sin(sensingAngle));
    vec2 sensingCenter = agent.position + sensingDir * sensorDist;
    ivec2 center = ivec2(int(sensingCenter.x),int(sensingCenter.y));
    vec3 sum = vec3(0,0,0);
    for(int offsetX=-sensorSize; offsetX <= sensorSize; offsetX++)
        for(int offsetY=-sensorSize; offsetY <= sensorSize; offsetY++)
        {
            ivec2 sensePixel = center + ivec2(offsetX, offsetY);
            itorus(sensePixel);
            sum.r += imageLoad(inRed, sensePixel).r;
            sum.g += imageLoad(inGreen, sensePixel).r;
            sum.b += imageLoad(inBlue, sensePixel).r;
        }

    uint r = 2*sensorSize+1;
    return sum / (r*r);
}

float[9] prepare_input(Agent agent)
{
    float inp[9];

    float dev = 0.25;

    float angles[3] = float[] ( -dev*M_PI , 0, +dev*M_PI );
    for(int a=0; a<3; a++)
    {
        vec3 sensor = sense(agent, angles[a], 3, 1);
        inp[a*3+0] = sensor.g;
        inp[a*3+1] = sensor.b;
        inp[a*3+2] = sensor.r;
    }

    return inp;
}

void update_one(uint idx)
{
    Agent agent = agents[idx];


    float[] inp = prepare_input(agent);


    float angleForce = 0;

    float activity = 0;

    for(int t=0; t<3; t++)
    {
        float left =    inp[0 + t];
        float front =   inp[3 + t];
        float right =   inp[6 + t];

        float d1 = front - left;
        float d2 = right - front;
        //float delta = right-left;
        float delta = abs(d1) > abs(d2) ? d1 : d2;


        float w = (agent.type == t) ? 2.0 : -2.0;

        angleForce += delta * w * 1;
       
        //angleDelta += tanh(delta * w )*1.0;

        if (agent.type == t)
          activity = max(front, max(left, right));

    }


    float random01 = get_random_float_01(agent, 100037);
    float random0 = (0.5 - random01);

    angleForce += 0.15 * random0;

    //angle
    
    agent.deltaAngle += angleForce * 1.5;
    agent.deltaAngle *= 0.8;
    agent.angle += agent.deltaAngle * 0.3;
    agent.angle = mod(agent.angle + M_PI, 2*M_PI) - M_PI;

    


    float velocity = 0.3;
    //float velocity = 0.1 + 0.2 * clamp(activity, 0.0, 1.0);
    agent.position += vec2(velocity*cos(agent.angle),velocity*sin(agent.angle));
    


    torus(agent.position);
    agent.currPixel = ivec2(uint(agent.position.x), uint(agent.position.y));
    agents[idx] = agent;

}

void main()
{
    uint idx = gl_GlobalInvocationID.x;

    if (idx >=0 && idx < config.agentsCount) 
    {
        update_one(idx);
        if (idx == config.trackedIdx)
            tracked = agents[idx];
    }
}
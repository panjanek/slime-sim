#version 430 core

#pragma optimize(on)

layout(local_size_x = {LocalSizeX}) in;

struct ShaderConfig {
    int agentsCount;
    int width;
    int height;
    float dt;
    float t;
    int generationDuration;
    float initialEnergy;
    float plantEnergy;
    float killEnergy;
    int plantRegrowDuration;
    int trackedIdx;
    float blueMaxVelocity;
    float redMaxVelocity;
    int pad0;
    int pad1;
    int pad2;
};

struct Agent
{
    vec2 position;
    float angle;
    uint type;
    float energy;
    uint age;
    int state;
    int nnOffset;
    int meals;
    int deaths;
    float energySpent;
    int flag;
    ivec2 currPixel;
    ivec2 prevPixel;
    float memory0;
    float memory1;
    float nearPrey;
    uint survivalDuration;
};

layout(std430, binding = 0) buffer ConfigBuffer {
    ShaderConfig config;
};

layout(std430, binding = 1) buffer AgentsBuffer {
    Agent agents[];
};

layout(rgba32f, binding = 2) uniform image2D inGreen;
layout(rgba32f, binding = 3) uniform image2D inBlue;
layout(rgba32f, binding = 4) uniform image2D inRed;

uint hash(uint state)
{
    state ^= 2747636419u;
    state *= 2654435769u;
    state ^= state >> 16;
    state *= 2654435769u;
    state ^= state >> 16;
    state *= 2654435769u;
    return state;
}

float scaleToRange01(uint state)
{
    return state / 4294967295.0;
}

float get_random_float_01(Agent agent, uint seed)
{
     uint random = hash(uint(agent.position.y * config.width + agent.position.x) + hash(gl_GlobalInvocationID.x + int(config.t) * seed));
     float random01 = scaleToRange01(random);
     return random01;
}

void update_one(uint idx)
{
    Agent agent = agents[idx];

    if (agent.state == 1)
        return;

    ivec2 pixelPos = ivec2(uint(agent.position.x), uint(agent.position.y));
    if (agent.type == 0) //plant
    {
        vec4 blue = imageLoad(inBlue, pixelPos);
        if (blue.r == 1)
        {
            agent.state = 1;
            agent.age = -config.plantRegrowDuration;
            agent.position = vec2(config.width * get_random_float_01(agent, 100037), config.height * get_random_float_01(agent, 20347));
        }
    }
    else if (agent.type == 1) // prey
    {
         vec4 green = imageLoad(inGreen, pixelPos);
         if (green.r == 1)
         {
             agent.energy += config.plantEnergy;
             agent.meals++;
         }

         vec4 red = imageLoad(inRed, pixelPos);
         if (red.r == 1)
         {
             agent.deaths++;
             agent.energy = 0;
             agent.survivalDuration = 0;
             agent.position = vec2(config.width * get_random_float_01(agent, 100037), config.height * get_random_float_01(agent, 20347));
             agent.currPixel = ivec2(uint(agent.position.x), uint(agent.position.y));
             agent.prevPixel = agent.currPixel;
             agent.memory0 = 0;
             agent.memory1 = 0;
         }
    }
    else if (agent.type == 2) // predator
    {
        vec4 blue = imageLoad(inBlue, pixelPos);
        if (blue.r == 1)
        {
            agent.energy += config.killEnergy;
            agent.meals++;
        }
    }

    agents[idx] = agent;
}
    

void main()
{
    uint idx = gl_GlobalInvocationID.x;

    if (idx >=0 && idx < config.agentsCount) 
    {
        update_one(idx);
    }
}